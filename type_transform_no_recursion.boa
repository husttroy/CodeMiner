# type transformations for each method
p:Project = input;
trans: output collection[string] of string;

cur_file: string;
cur_class: string;
cur_method: string;

# API query
api_query : array of string;
api_query = {"createNewFile"};

# type query
type_query : array of string;
type_query = {"File"};

# a coarse-grained query methodology based on a given set of types and apis
query := function(m: Method) : bool{
    # referenced types in a method
    types: set of string;
    # referenced apis in a method
    apis: set of string;
    
    visit(m, visitor{
        before node: Variable -> add(types, node.variable_type.name);
        
        before node: Expression -> {
            if (node.kind == ExpressionKind.METHODCALL) {
                add(apis, node.method);
            }
        }
    });
    
    # API query
    foreach(i : int; def(api_query[i])) {
        if (!contains(apis, api_query[i])) {
            return false;
        }
    }
    
    # type query
    foreach(i : int; def(type_query[i])) {
        if (!contains(types, type_query[i])) {
            return false;
        }
    }
    
    return true;
};

# symbol table for fields : var -> type
field_table: map[string] of string;
    
# symbol table for local variables and arguments : var -> type
local_table: map[string] of string;

# assignment mapping, record the data flow
assign: map[string] of string;

# call-on table : var -> m1||m2||m3
callOn: map[string] of string;

# call-on-rev table : method call -> v1,v2,v3
revCallOn: map[string] of string;

# dataflow table : var -> v1,v2,v3
dataflow: map[string] of string;

# method call ordering : m1||m2.m3||m4(m5)
order: string;

# predicates : method call -> p1*p2*p3
predicate: map[string] of string;


# takes a method call expression and return a method call sequnce in the format of m1.m2.m3...
get_method_seq := function(expr: Expression) : string {
    seq: = "";
    st : stack of string;
    
    visit(expr, visitor{
        before node: Expression -> {
            switch (node.kind) {
                case ExpressionKind.METHODCALL:
                    if (def(node.expressions) && len(node.expressions) == 1) {
		                if (node.expressions[0].kind == ExpressionKind.VARACCESS) {
		    	            # first method call
		    	            seq = node.method;
			                # append the rest of method calls in the sequence, if any
		    	            while (def(peek(st))) {
			                    # method call sequence			                    
			                    seq = format("%s.%s", seq, pop(st));
		    	            }
			                # do not visit other nodes such as arguments and receiver
			                stop;
		                } else if (node.expressions[0].kind == ExpressionKind.METHODCALL) {
                    	    # this is a sequence of method calls
			                push(st, node.method);
			                # only visit the following method call(s)
			                visit(node.expressions[0]);
			                # do not visit other nodes such as arguments
			                stop;
		                }
		            } else if (!def(node.expressions)) {
		                # local method call
		                seq = node.method;
		                # append the rest of method calls in the sequence, if any
		                while (def(peek(st))) {
			                # method call sequence
			                seq = format("%s.%s", seq, pop(st));
		                }
		                # do not visit other nodes such as arguments
		                stop;
		            }
		            break;
		        case ExpressionKind.NEW:
		            # constructer is either called first or called as an argument (ignore argument for now)
		            seq = format("new %s", node.new_type.name);
		            
		            while (def(peek(st))) {
		                seq = format("%s.%s", seq, pop(st));
		            }
		            
                    stop;
                default: break;
            }
        }
    });
    
    return seq;
};

get_predicate := function(expr: Expression) : string {
    cond := "";
    st : stack of string;
    
    visit(expr, visitor{
        before node: Expression -> {
            switch (node.kind) {
                case ExpressionKind.METHODCALL:
                    cond = format("%s%s", cond, get_method_seq(node));
		            stop;
		        case ExpressionKind.NEW:
		            cond = format("%s%s", cond, get_method_seq(node));
		            break;
		        case ExpressionKind.LOGICAL_NOT:
		            cond = format("%s!", cond);
		            break;
		        case ExpressionKind.VARACCESS:
		            cond = format("%s%s", cond, node.variable);
		            break;
		        case ExpressionKind.LITERAL:
		            cond = format("%s%s", cond, node.literal);
		            break;
		        case ExpressionKind.LOGICAL_AND:
		            if (def(node.expressions) && len(node.expressions) == 2) {
		                visit(node.expressions[0]);
		                cond = format("%s && ", cond);
		                visit(node.expressions[1]);
		                stop;
		            }
		            break;
		        case ExpressionKind.LOGICAL_OR:
		            if (def(node.expressions) && len(node.expressions) == 2) {
		                visit(node.expressions[0]);
		                cond = format("%s || ", cond);
		                visit(node.expressions[1]);
		                stop;
		            }
		            break;
		        case ExpressionKind.EQ:
		            if (def(node.expressions) && len(node.expressions) == 2) {
		                visit(node.expressions[0]);
		                cond = format("%s == ", cond);
		                visit(node.expressions[1]);
		                stop;
		            }
		            break;
		        case ExpressionKind.LT:
		            if (def(node.expressions) && len(node.expressions) == 2) {
		                visit(node.expressions[0]);
		                cond = format("%s < ", cond);
		                visit(node.expressions[1]);
		                stop;
		            }
		            break;
		        case ExpressionKind.LTEQ:
		            if (def(node.expressions) && len(node.expressions) == 2) {
		                visit(node.expressions[0]);
		                cond = format("%s <= ", cond);
		                visit(node.expressions[1]);
		                stop;
		            }
		            break;
		        case ExpressionKind.NEQ:
		            if (def(node.expressions) && len(node.expressions) == 2) {
		                visit(node.expressions[0]);
		                cond = format("%s != ", cond);
		                visit(node.expressions[1]);
		                stop;
		            }
		            break;
		        case ExpressionKind.GT:
		            if (def(node.expressions) && len(node.expressions) == 2) {
		                visit(node.expressions[0]);
		                cond = format("%s > ", cond);
		                visit(node.expressions[1]);
		                stop;
		            }
		            break;
		        case ExpressionKind.GTEQ:
		            if (def(node.expressions) && len(node.expressions) == 2) {
		                visit(node.expressions[0]);
		                cond = format("%s >= ", cond);
		                visit(node.expressions[1]);
		                stop;
		            }
		            break;
		        default: break;
            }
        }
    });
    
    return cond;
};

traverse_method := function(m: Method) {
    key := format("%s ** %s ** %s ** %s", p.project_url, cur_file, cur_class, cur_method);
    
    stts: string = "";
    
    # arguments
    foreach (i: int; def(m.arguments[i])){
        arg := m.arguments[i];
        local_table[arg.name] = arg.variable_type.name;
    }
    
    st : stack of string;
    isPredicate := false;
    visit(m, visitor{
        before node: Variable -> {
            local_table[node.name] = node.variable_type.name;
            
            if(def(node.initializer)){
                # assignment mapping
                
            }
        }
        
        before node: Statement -> {
            if (node.kind == StatementKind.IF && def(node.expression)) {
                # visit if predicate first
                isPredicate = true;
                #stts = format("%s||IF(", stts);
                #visit(node.expression);
                #stts = format("%s){", stts);
                stts = format("%s||IF(%s){", stts, get_predicate(node.expression));
                isPredicate = false;
                
                foreach (i: int; def(node.statements[i])) {
                    visit(node.statements[i]);
                }
            
                stts = format("%s}", stts);
                stop;
            }
        }
        
        before node: Expression -> {
            switch (node.kind) {
                case ExpressionKind.METHODCALL:
		            stts = format("%s||%s", stts, get_method_seq(node));
		            break;
                case ExpressionKind.NEW:
                    stts = format("%s||%s", stts, get_method_seq(node));
                    break;
                case ExpressionKind.LOGICAL_NOT:
                    if (isPredicate) {
                        stts = format("%s!", stts); 
                    }
                    break;
                default: break;
            }
        }
    });
    
    trans[key] << stts;
    
    clear(local_table);
};

visit(p, visitor {
    # only look at the latest snapshot
    before node: CodeRepository -> {
        # ignore projects with less than 100 revisions
        if (len(node.revisions) < 100) stop;
        
        snapshot := getsnapshot(node, "SOURCE_JAVA_JLS");
        foreach (i: int; def(snapshot[i]))
			visit(snapshot[i]);
		stop;
    }
    
    # record the current file
    before node: ChangedFile -> {
        cur_file = node.name;
        visit(node, visitor{
            
            # visit class declaration
            before node: Declaration -> {
                # record the class name
                cur_class = node.name;
                
                foreach(i: int; def(node.fields[i])){
                    f := node.fields[i];
                    field_table[f.name] = f.variable_type.name; # store the type of fields
                }
                
                foreach(i: int; def(node.methods[i])){
                    m := node.methods[i];
                    cur_method = m.name;
                    
                    if (query(m)) {
                        #trans[p.id] << format("%s ** %s ** %s ** %s", p.project_url, cur_file, cur_class, cur_method);
                        traverse_method(m);
                    }
                }
            }
            
            # clear field table just in case there are nested class declarations
            after node: Declaration -> {
                clear(field_table);
            }
        });
    }
});
